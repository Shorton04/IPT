FUNCTION zpopup_table_disp_with_button.
*"----------------------------------------------------------------------
*"*"Local Interface:
*"  IMPORTING
*"     VALUE(IT_VALUE) TYPE  CRMT_IC_STRING_TAB
*"     VALUE(IV_BUTTON1_TEXT) TYPE  CHAR30 OPTIONAL
*"     VALUE(IV_BUTTON2_TEXT) TYPE  CHAR30 OPTIONAL
*"     VALUE(IV_BUTTON3_TEXT) TYPE  CHAR30 OPTIONAL
*"     VALUE(IV_BUTTON4_TEXT) TYPE  CHAR30 OPTIONAL
*"     VALUE(IV_BUTTON5_TEXT) TYPE  CHAR30 OPTIONAL
*"     VALUE(IV_ENDPOS_COL) TYPE  INT4 OPTIONAL
*"     VALUE(IV_ENDPOS_ROW) TYPE  INT4 OPTIONAL
*"     VALUE(IV_STARTPOS_COL) TYPE  INT4 OPTIONAL
*"     VALUE(IV_STARTPOS_ROW) TYPE  INT4 OPTIONAL
*"     VALUE(IV_TITLETEXT) TYPE  CHAR80
*"  EXPORTING
*"     REFERENCE(EV_INDEX) TYPE  SY-TABIX
*"     REFERENCE(EV_ACTION) TYPE  INT4
*"  EXCEPTIONS
*"      NO_VALUE_TO_PRINT
*"      NO_ACTION
*"----------------------------------------------------------------------

  CLEAR: ev_index, ev_action,
         gv_action, gv_index.

  IF it_value IS INITIAL.
    RAISE no_value_to_print.
  ENDIF.
  IF iv_button1_text IS INITIAL
    AND iv_button2_text IS INITIAL
    AND iv_button3_text IS INITIAL
    AND iv_button4_text IS INITIAL
    AND iv_button5_text IS INITIAL.
    raise no_action.
  ENDIF.

  gt_value = it_value.
  gv_button1_text = iv_button1_text.
  gv_button2_text = iv_button2_text.
  gv_button3_text = iv_button3_text.
  gv_button4_text = iv_button4_text.
  gv_button5_text = iv_button5_text.
  gv_title_text = iv_titletext.

  IF iv_startpos_col IS INITIAL.
    iv_startpos_col = 10.
  ENDIF.
  IF iv_startpos_row IS INITIAL.
    iv_startpos_row = 5.
  ENDIF.

  DESCRIBE TABLE gt_value LINES DATA(lv_rec_count).

  IF iv_endpos_row IS INITIAL.
    IF lv_rec_count > 20.
      iv_endpos_row = iv_startpos_row + 20.
    ELSE.
      iv_endpos_row = iv_startpos_row + lv_rec_count + 1.
    ENDIF.
  ENDIF.

  DATA: lv_prev_length TYPE int4,
        lv_length      TYPE int4.

  IF iv_endpos_col IS INITIAL.
    LOOP AT gt_value INTO DATA(lv_value).
      lv_length = strlen( lv_value ).
      IF lv_prev_length < lv_length.
        lv_prev_length = lv_length.
      ENDIF.
    ENDLOOP.

    IF lv_prev_length > 180.
      iv_endpos_col = 180.
    ELSE.
      iv_endpos_col = lv_prev_length + iv_startpos_col.
    ENDIF.
  ENDIF.

  CALL SCREEN 0100 STARTING AT iv_startpos_col iv_startpos_row
                   ENDING   AT iv_endpos_col iv_endpos_row.

  ev_index = gv_index.
  ev_action = gv_action.

ENDFUNCTION.
